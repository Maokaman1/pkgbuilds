pkgname=('emby-server')
srcname='Emby'
pkgdesc='Emby Server is a home media server built on top of other popular open source technologies'
pkgver='3.0.5972'
pkgrel='1'
arch=('i686' 'x86_64')
url='https://github.com/MediaBrowser/Emby'
license=('GPL2')

depends=('ffmpeg' 'imagemagick' 'mono' 'sqlite')
makedepends=('git')
provides=('emby-server')
conflicts=('emby-server')

source=(
    "${srcname}::git+${url}#tag=${pkgver}"
    'emby-server'
    'emby-migrate-database'
    'emby-server.conf'
    'emby-server.service'
    'register.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

install='emby-server.install'
backup=('etc/conf.d/emby-server')

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/register.patch"

    sed 's/libMagickWand-6.Q8.so/libMagickWand-6.Q16HDRI.so/' -i MediaBrowser.Server.Mono/ImageMagickSharp.dll.config
}

build() {
    cd "${srcdir}/${srcname}"

    xbuild \
    /p:Configuration='Release Mono' \
    /p:Platform='Any CPU' \
    /p:OutputPath="${srcdir}/build" \
    /t:build MediaBrowser.Mono.sln
    mono --aot='full' -O='all' ../build/MediaBrowser.Server.Mono.exe
}

package() {
    cd "${srcdir}"

    install -dm 755 "${pkgdir}"/{etc/conf.d,usr/{bin,lib/systemd/system}}
    cp -dr --no-preserve='ownership' build "${pkgdir}"/usr/lib/emby-server
    find "${pkgdir}" -type f -name *.dylib -delete
    install -m 755 emby-server "${pkgdir}"/usr/bin/
    install -m 755 emby-migrate-database "${pkgdir}"/usr/bin/
    install -m 644 emby-server.service "${pkgdir}"/usr/lib/systemd/system/
    install -m 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server

    install -dm 755 "${pkgdir}"/var/lib/emby
    chown 422:422 -R "${pkgdir}"/var/lib/emby
}
