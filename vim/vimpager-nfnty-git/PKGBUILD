# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('vimpager-nfnty-git')
_srcname='vimpager'
pkgver='r1'
pkgrel='1'
pkgdesc='Vim as pager with escape sequences stripped'
arch=('any')
url='https://github.com/nfnty/vimpager'
license=('MIT')

makedepends=('git')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"
    install -D default.sh "${pkgdir}/usr/bin/pager"
    install -D man.sh "${pkgdir}/usr/bin/pager-man"
    install -D diff.sh "${pkgdir}/usr/bin/pager-diff"
    install -D gitlog.sh "${pkgdir}/usr/bin/pager-gitlog"
}
