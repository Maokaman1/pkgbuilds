# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('neovim-git')
_srcname='neovim'
pkgdesc='Vim-fork focused on extensibility and agility'
pkgver='r1'
pkgrel='1'
arch=('x86_64')
url="https://github.com/nfnty/${_srcname}"
license=('custom:neovim')

depends=('libtermkey' 'libuv' 'libvterm' 'msgpack-c' 'unibilium')
optdepends=(
    'python2-neovim: for Python 2 plugin support (see :help provider-python)'
    'python-neovim: for Python 3 plugin support (see :help provider-python)'
    'xclip: for clipboard support (or xsel) (see :help provider-clipboard)'
    'xsel: for clipboard support (or xclip) (see :help provider-clipboard)'
)
makedepends=('git' 'cmake' 'gperf' 'luajit' 'lua51-mpack' 'lua51-lpeg' 'clang')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

options=('!strip' 'debug')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    mkdir build
    cd build

    export CC='/usr/bin/clang'
    export CXX='/usr/bin/clang++'
    export CFLAGS='-march=x86-64'
    export CXXFLAGS='-march=x86-64'
    export DEBUG_CFLAGS='-g'
    export DEBUG_CXXFLAGS='-g'
    cmake \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE='Debug' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        ..

    make
}

check() {
    cd "${srcdir}/${_srcname}/build"
    ./bin/nvim --version
    ./bin/nvim --headless -u NONE -i NONE -c ':quit'
}

package() {
    cd "${srcdir}/${_srcname}/build"

    make DESTDIR="${pkgdir}" install
}
