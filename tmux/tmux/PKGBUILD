# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('tmux')
_srcname='tmux'
pkgdesc='Terminal multiplexer'
pkgver='2.2'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/tmux/${_srcname}"
license=('ISC')

depends=('ncurses' 'libevent')
makedepends=('git')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${_srcname}::git+${url}.git#tag=${pkgver}"
    'git+https://github.com/przepompownia/tmux-bash-completion.git'
    'ixon.patch'
    'pane_is_dead.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

prepare() {
    cd "${srcdir}/${_srcname}"

    git apply "${srcdir}/ixon.patch"
    git apply "${srcdir}/pane_is_dead.patch"
}

build() {
    cd "${srcdir}/${_srcname}"

    ./autogen.sh
    ./configure --prefix='/usr'

    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install

    install -D --mode='644' "${srcdir}/tmux-bash-completion/completions/tmux" \
        "${pkgdir}/usr/share/bash-completion/completions/tmux"
}
