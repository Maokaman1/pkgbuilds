pkgname=('avidemux-cli-git' 'avidemux-qt-git')
srcname='avidemux2'
pkgbase='avidemux-git'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/mean00/${srcname}"
license=('GPL2')

makedepends=(
    'git'

    'cmake'
    'libpng'
    'sqlite'
    'yasm'

    'a52dec'
    'opencore-amr'
    'libdca'
    'faad2'
    'spandsp'
    'ffmpeg'
    'libmad'
    'opus'
    'libvorbis'

    'alsa-lib'
    'jack2'
    'libpulse'

    # 'dcaenc'
    'faac'
    'lame'
    'libogg'
    'twolame'

    # 'vapoursynth'

    'libvpx'

    'xvidcore'
    'libx264'
    'x265'

    'fontconfig'
    'freetype2'
    'fribidi'

    'glu'
    'libva'
    'libvdpau'

    'libxv'
    'qt5-base'
    'qt5-tools'
)

source=("${srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

_make() {
    mkdir --parents "${1}"
    cd "${1}"
    cmake "${args_base[@]}" "${@:3}" "${2}"
    make
    make DESTDIR="${path_fakeroot}" install
}

build() {
    local path_git="${srcdir}/${srcname}"
    local path_build="${srcdir}/build"
    path_fakeroot="${srcdir}/fakeroot"
    args_base=(
        '-DCMAKE_BUILD_TYPE=Release'
        '-DCMAKE_INSTALL_PREFIX=/usr'
        "-DFAKEROOT=${path_fakeroot}"
    )
    local args_plugin=(
        "-DAVIDEMUX_SOURCE_DIR=${path_git}"
    )

    # core
    _make "${path_build}/core" "${path_git}/avidemux_core"
    _make "${path_build}/plugins_common" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='COMMON'
    _make "${path_build}/plugins_settings" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='SETTINGS'
    # cli
    _make "${path_build}/cli" "${path_git}/avidemux/cli"
    _make "${path_build}/plugins_cli" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='CLI'
    # qt
    _make "${path_build}/qt" "${path_git}/avidemux/qt4" -DENABLE_QT5='ON'
    _make "${path_build}/plugins_qt" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='QT4' -DENABLE_QT5='ON'
}

package_avidemux-cli-git() {
    pkgdesc='Video editor designed for simple cutting, filtering and encoding tasks'
    provides=('avidemux-cli')
    conflicts=('avidemux-cli')
    depends=(
        'libpng'
        'sqlite'

        'a52dec'
        'opencore-amr'
        'libdca'
        'faad2'
        'spandsp'
        'ffmpeg'
        'libmad'
        'opus'
        'libvorbis'

        'alsa-lib'
        'jack2'
        'libpulse'

        # 'dcaenc'
        'faac'
        'lame'
        'libogg'
        'twolame'

        # 'vapoursynth'

        'libvpx'

        'xvidcore'
        'libx264'
        'x265'

        'fontconfig'
        'freetype2'
        'fribidi'

        'glu'
        'libva'
        'libvdpau'
    )
    optdepends=('avidemux-qt-git: QT GUI')

    for target in 'core' 'plugins_common' 'plugins_settings' 'cli' 'plugins_cli'; do
        cd "${srcdir}/build/${target}"
        make DESTDIR="${pkgdir}" install
    done
}

package_avidemux-qt-git() {
    pkgdesc='Video editor designed for simple cutting, filtering and encoding tasks - QT GUI'
    provides=('avidemux-qt')
    conflicts=('avidemux-qt')
    depends=(
        'avidemux-cli-git'

        'libxv'
        'qt5-base'

        'desktop-file-utils'
    )
    install='install.sh'

    for target in 'qt' 'plugins_qt'; do
        cd "${srcdir}/build/${target}"
        make DESTDIR="${pkgdir}" install
    done

    install -D --mode='644' \
        "${srcdir}/${srcname}/avidemux2.desktop" \
        "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
    install -D --mode='644' \
        "${srcdir}/${srcname}/avidemux_icon.png" \
        "${pkgdir}/usr/share/pixmaps/avidemux-qt.png"
    sed --in-place \
        --expression='s/^Icon=avidemux$/Icon=avidemux-qt/' \
        --expression='s/^Exec=avidemux2_gtk$/Exec=avidemux3_qt5/' \
        --expression='s/^Name=avidemux2$/Name=Avidemux QT/' \
        "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
}
