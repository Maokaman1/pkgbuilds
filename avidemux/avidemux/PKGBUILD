pkgname=('avidemux-cli' 'avidemux-qt')
_srcname='avidemux2'
pkgbase='avidemux'
pkgver='2.6.12'
pkgrel='1'
arch=('i686' 'x86_64')
url='http://www.avidemux.org'
license=('GPL2')

makedepends=(
    'git'

    'cmake'
    'libpng'
    'sqlite'
    'yasm'

    'a52dec'
    'opencore-amr'
    'libdca'
    'faad2'
    'spandsp'
    'ffmpeg'
    'libmad'
    'opus'
    'libvorbis'

    'alsa-lib'
    'jack2'
    'libpulse'

    # 'dcaenc'
    'faac'
    'lame'
    'libogg'
    'twolame'

    # 'vapoursynth'

    'libvpx'

    'xvidcore'
    'libx264'
    'x265'

    'fontconfig'
    'freetype2'
    'fribidi'

    'glu'
    'libva'
    'libvdpau'

    'libxv'
    'qt5-base'
    'qt5-tools'
)

# Version commits are tracked via `cmake/avidemuxVersion.cmake`
source=(
    "${_srcname}::git+https://github.com/mean00/${_srcname}.git#commit=856fc8ae1ec8ea03595c04153b42423fa783b55d"
    'cxx-11.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
)

prepare() {
    cd "${srcdir}/${_srcname}"

    git cherry-pick --no-commit \
        'b54303bc9b9310e288a1dde9c923f48e854bd55f' \
        '68df30b902540e6f299dc63ea3ae53b3d353cbe4' \
        '6ad028d9f9caf929819c1afcb693611aade0af0b' \
        '7dee579d3ac16d731898305d24bb771834ae0ce0'
    git apply "${srcdir}/cxx-11.patch"
}

_make() {
    mkdir --parents "${1}"
    cd "${1}"
    cmake "${args_base[@]}" "${@:3}" "${2}"
    make
    make DESTDIR="${path_fakeroot}" install
}

build() {
    local path_git="${srcdir}/${_srcname}"
    local path_build="${srcdir}/build"
    path_fakeroot="${srcdir}/fakeroot"
    args_base=(
        '-DCMAKE_BUILD_TYPE=Release'
        '-DCMAKE_INSTALL_PREFIX=/usr'
        "-DFAKEROOT=${path_fakeroot}"
    )
    local args_plugin=(
        "-DAVIDEMUX_SOURCE_DIR=${path_git}"
    )

    # core
    _make "${path_build}/core" "${path_git}/avidemux_core"
    _make "${path_build}/plugins_common" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='COMMON'
    _make "${path_build}/plugins_settings" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='SETTINGS'
    # cli
    _make "${path_build}/cli" "${path_git}/avidemux/cli"
    _make "${path_build}/plugins_cli" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='CLI'
    # qt
    _make "${path_build}/qt" "${path_git}/avidemux/qt4" -DENABLE_QT5='ON'
    _make "${path_build}/plugins_qt" "${path_git}/avidemux_plugins" "${args_plugin[@]}" -DPLUGIN_UI='QT4' -DENABLE_QT5='ON'
}

package_avidemux-cli() {
    pkgdesc='Video editor designed for simple cutting, filtering and encoding tasks'
    provides=('avidemux-cli')
    conflicts=('avidemux-cli')
    depends=(
        'libpng'
        'sqlite'

        'a52dec'
        'opencore-amr'
        'libdca'
        'faad2'
        'spandsp'
        'ffmpeg'
        'libmad'
        'opus'
        'libvorbis'

        # 'dcaenc'
        'faac'
        'lame'
        'libogg'
        'twolame'

        # 'vapoursynth'

        'libvpx'

        'xvidcore'
        'libx264'
        'x265'

        'fontconfig'
        'freetype2'
        'fribidi'
    )
    optdepends=(
        'avidemux-qt: QT GUI'

        'alsa-lib: Audio device plugin'
        'jack2: Audio device plugin'
        'libpulse: Audio device plugin'

        'libva: Hardware acceleration'
        'libvdpau: Hardware acceleration'
    )

    for target in 'core' 'plugins_common' 'plugins_settings' 'cli' 'plugins_cli'; do
        cd "${srcdir}/build/${target}"
        make DESTDIR="${pkgdir}" install
    done
}

package_avidemux-qt() {
    pkgdesc='Video editor designed for simple cutting, filtering and encoding tasks - QT GUI'
    provides=('avidemux-qt')
    conflicts=('avidemux-qt')
    depends=(
        'avidemux-cli'

        'glu'
        'libxv'
        'qt5-base'

        'desktop-file-utils'
    )
    install='install.sh'

    for target in 'qt' 'plugins_qt'; do
        cd "${srcdir}/build/${target}"
        make DESTDIR="${pkgdir}" install
    done

    install -D --mode='644' "${srcdir}/${_srcname}/avidemux2.desktop" \
        "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
    install -D --mode='644' "${srcdir}/${_srcname}/avidemux_icon.png" \
        "${pkgdir}/usr/share/pixmaps/avidemux-qt.png"
    sed --in-place \
        --expression='s/^Icon=avidemux$/Icon=avidemux-qt/' \
        --expression='s/^Exec=avidemux2_gtk$/Exec=avidemux3_qt5/' \
        --expression='s/^Name=avidemux2$/Name=Avidemux QT/' \
        "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
}
