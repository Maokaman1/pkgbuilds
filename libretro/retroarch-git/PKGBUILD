# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('retroarch-git')
_srcname='RetroArch'
pkgdesc='Reference frontend for the libretro API'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/libretro/${_srcname}"
license=('GPL3')

depends=('libgl' 'alsa-lib' 'freetype2' 'libxml2' 'nvidia-cg-toolkit')
makedepends=('git')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    ./configure --prefix='/usr'

    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install

    install --mode='755' --target-directory="${pkgdir}/usr/bin" tools/retroarch-joyconfig
}
