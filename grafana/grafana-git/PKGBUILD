# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('grafana-git')
_srcname='grafana'
_pkgbasename="${pkgname[0]%-git}"
pkgdesc='Feature rich metrics dashboard and graph editor'
pkgver='r9117'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/grafana/${_srcname}"
license=('Apache')

makedepends=('git' 'go' 'npm' 'python2')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

_url_go="${url#*//}"

pkgver() {
    export GOPATH="${srcdir}/gopath"
    cd "${GOPATH}/src/${_url_go}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    ln --symbolic --relative --force "${srcdir}/${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${url}"
    go run build.go setup
    "${GOPATH}/bin/govendor" sync

    npm install
}

build() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    go run build.go build

    npm run build
}

package() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/"{"etc/${_pkgbasename}",usr/bin,"usr/share/${_pkgbasename}","var/lib/${_pkgbasename}"}
    install --target-directory="${pkgdir}/usr/bin" bin/grafana-cli bin/grafana-server

    install -D --mode='644' conf/defaults.ini "${pkgdir}/usr/share/${_pkgbasename}/conf/defaults.ini"
    install -D --mode='644' conf/sample.ini "${pkgdir}/etc/${_pkgbasename}/grafana.ini"

    install --directory "${pkgdir}/usr/share/${_pkgbasename}/vendor"
    cp --recursive vendor/phantomjs "${pkgdir}/usr/share/${_pkgbasename}/vendor/"
    cp --recursive public_gen "${pkgdir}/usr/share/${_pkgbasename}/public"

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}"
}
