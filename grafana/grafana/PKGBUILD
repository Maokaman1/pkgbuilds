# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('grafana')
_srcname='grafana'
_pkgbasename="${pkgname[0]%-git}"
pkgdesc='Feature rich metrics dashboard and graph editor'
pkgver='5.3.2'
_commit='0d821d07adbd82a502dfb37ef533ea549cb2ff14'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/grafana/${_srcname}"
license=('Apache')

makedepends=('git' 'go' 'npm' 'python2' 'yarn')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    ln --symbolic --relative --force "${srcdir}/${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${url}"
    go run build.go setup
    git checkout --quiet "${_commit}"

    npm install
}

build() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    go run build.go build

    yarn install
    npm run build release
}

package() {
    cd "${srcdir}/${_srcname}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/"{"etc/${_pkgbasename}",usr/bin,"usr/share/${_pkgbasename}","var/lib/${_pkgbasename}"}
    install --target-directory="${pkgdir}/usr/bin" bin/linux-amd64/grafana-cli bin/linux-amd64/grafana-server

    install -D --mode='644' conf/defaults.ini "${pkgdir}/usr/share/${_pkgbasename}/conf/defaults.ini"
    install -D --mode='644' conf/sample.ini "${pkgdir}/etc/${_pkgbasename}/grafana.ini"

    for dir in vendor public tools; do
        cp --recursive "${dir}" "${pkgdir}/usr/share/${_pkgbasename}/"
    done

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}"
}
