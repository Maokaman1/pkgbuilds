# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('parity')
_srcname='parity'
pkgdesc='Fast, light, robust Ethereum implementation'
pkgver='1.8.10'
_commit='6c50f9007a61339597edb30ea03dcf4b90a6838b'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/paritytech/${_srcname}"
license=('GPL3')

depends=('openssl' 'libsystemd')
makedepends=('git' 'rust' 'cargo' 'systemd' 'gcc' 'pkg-config')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git#commit=${_commit}")
sha512sums=('SKIP')

build() {
    cd "${srcdir}/${_srcname}"

    export CARGO_HOME="${srcdir}/cargo"
    cargo build --verbose --release
}

package() {
    cd "${srcdir}/${_srcname}"

    install -D --mode=755 target/release/parity "${pkgdir}/usr/bin/parity"
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licences/${pkgname[0]%-git}/LICENSE"
}
