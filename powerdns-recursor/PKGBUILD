# $Id: PKGBUILD 100035 2013-10-31 11:20:48Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>

pkgname=powerdns-recursor
pkgver=4.0.1
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
license=('GPL')
arch=('x86_64' 'i686')
depends=('gcc-libs' 'lua' 'openssl' 'boost-libs')
makedepends=('boost' 'inetutils')
replaces=('pdns-recursor')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(https://downloads.powerdns.com/releases/pdns-recursor-$pkgver.tar.bz2{,.asc}
        'pdns-recursor.service')
sha256sums=('472db541307c8ca83a846d260ecfc854fd8e879c1bb2ce5683a8df5d21e860b0'
            'SKIP'
            'd4f545e85dffa86bf23c89ff0f352c74ec3524dfc53871f129656299222e4fba')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7') # Pieter Lexis <pieter.lexis@powerdns.com>

build() {
  cd "pdns-recursor-$pkgver"

  ./configure --prefix=/usr --exec-prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc/powerdns
  make -j1 LUA=1 \
    LUA_CPPFLAGS_CONFIG="$(pkg-config --cflags lua)" \
    LUA_LIBS_CONFIG="$(pkg-config --libs lua)"
}

package() {
  cd "pdns-recursor-$pkgver"

  make DESTDIR="$pkgdir" install
  mv "$pkgdir/etc/powerdns/recursor.conf-dist" \
    "$pkgdir/etc/powerdns/recursor.conf"
  install -Dm644 "$srcdir/pdns-recursor.service" \
    "$pkgdir/usr/lib/systemd/system/pdns-recursor.service"
}

# vim:set ts=2 sw=2 et:
