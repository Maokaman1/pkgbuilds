# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('python2-telldus-git')
_srcname='telldus'
pkgver='r1'
pkgrel='1'
pkgdesc='Python 2 bindings for Telldus Technologies TellStick'
arch=('any')
url="https://github.com/telldus/${_srcname}"
license=('LGPL2')

depends=('python2' 'telldus-core')
makedepends=('git' 'python2-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${_srcname}::git+${url}.git"
)
sha512sums=(
    'SKIP'
)

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}/bindings/python/native"

    python2 setup.py install --root="${pkgdir}" --optimize=1
}
