pkgname=('python-tellcore-py-git')
_srcname='tellcore-py'
pkgdesc='Python wrapper for Telldus home automation library Telldus Core'
pkgver='r1'
pkgrel='1'
arch=('any')
url='https://github.com/erijo/tellcore-py'
license=('GPL3')

depends=('python' 'telldus-core')
makedepends=('git' 'python-setuptools')
provides=('tellcore-py')
conflicts=('tellcore-py')

source=("${_srcname}::git+https://github.com/erijo/tellcore-py.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
