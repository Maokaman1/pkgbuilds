pkgname=('dvorak-nfnty-git')
_srcname='dvorak-nfnty'
pkgver='r1'
pkgrel='1'
pkgdesc='Customized dvorak layout for console and Xorg'
arch=('any')
url='https://github.com/nfnty/dvorak-nfnty'
license=('MIT')

makedepends=('git' 'gzip')
provides=('dvorak-nfnty')
conflicts=('dvorak-nfnty')

source=("${_srcname}::git+https://github.com/nfnty/dvorak-nfnty.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    gzip --stdout 'kbd/nfnty_desk.map' > 'kbd/nfnty_desk.map.gz'
}

package() {
    cd "${srcdir}/${_srcname}"

    install -D --mode=644 'xkb/nfnty_desk' "${pkgdir}/usr/share/X11/xkb/symbols/nfnty_desk"
    install -D --mode=644 'xkb/nfnty_lap' "${pkgdir}/usr/share/X11/xkb/symbols/nfnty_lap"
    install -D --mode=644 'kbd/nfnty_desk.map.gz' "${pkgdir}/usr/share/kbd/keymaps/nfnty_desk.map.gz"
}
