pkgname=('python-mpd2-git')
_srcname='python-mpd2'
pkgdesc='Client interface for the Music Player Daemon'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/Mic92/${_srcname}"
license=('GPL3')

depends=('python')
makedepends=('git' 'python-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
