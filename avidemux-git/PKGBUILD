pkgname=('avidemux-cli-git' 'avidemux-qt-git')
srcname='avidemux'
pkgbase='avidemux-git'
pkgdesc='Simple video editor'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url='https://github.com/mean00/avidemux2'
license=('GPL2')

makedepends=(
    # 'aften'
    'cmake'
    # 'dcaenc'
    'faac'
    'faad2'
    'fribidi'
    'git'
    'glu'
    'jack2'
    'lame'
    'libdca'
    'libpulse'
    'libva'
    'libvpx'
    'libx264'
    'libxv'
    'opencore-amr'
    'qt5-base'
    'qt5-tools'
    'sdl'
    'sqlite'
    'twolame'
    'x265'
    'xvidcore'
    'yasm'
)

source=("${srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

_make() {
    mkdir --parents "${1}"
    cd "${1}"
    cmake "${@:4}" "${2}"
    make
    make DESTDIR="${3}" install
}

build() {
    path_git="${srcdir}/${srcname}"
    path_build="${srcdir}/build"
    path_fakeroot="${srcdir}/fakeroot"
    args_base=(
        '-DCMAKE_BUILD_TYPE=Release'
        '-DCMAKE_INSTALL_PREFIX=/usr'
        "-DFAKEROOT=${path_fakeroot}"
        '-DENABLE_QT5=ON'
    )
    args_plugin=(
        "-DAVIDEMUX_SOURCE_DIR=${path_git}"
    )

    _make "${path_build}/core" "${path_git}/avidemux_core" "${path_fakeroot}" \
        "${args_base[@]}"
    _make "${path_build}/core_plugins_common" "${path_git}/avidemux_plugins" "${path_fakeroot}" \
        "${args_base[@]}" "${args_plugin[@]}" -DPLUGIN_UI='COMMON'
    _make "${path_build}/core_plugins_settings" "${path_git}/avidemux_plugins" "${path_fakeroot}" \
        "${args_base[@]}" "${args_plugin[@]}" -DPLUGIN_UI='SETTINGS'
    _make "${path_build}/cli" "${path_git}/avidemux/cli" "${path_fakeroot}" \
        "${args_base[@]}"
    _make "${path_build}/cli_plugins" "${path_git}/avidemux_plugins" "${path_fakeroot}" \
        "${args_base[@]}" "${args_plugin[@]}" -DPLUGIN_UI='CLI'
    _make "${path_build}/qt" "${path_git}/avidemux/qt4" "${path_fakeroot}" \
        "${args_base[@]}"
    _make "${path_build}/qt_plugins" "${path_git}/avidemux_plugins" "${path_fakeroot}" \
        "${args_base[@]}" "${args_plugin[@]}" -DPLUGIN_UI='QT4'
}

package_avidemux-cli-git() {
    pkgdesc="Simple video editor"
    provides=('avidemux-cli')
    conflicts=('avidemux-cli')
    depends=(
        # 'aften'
        # 'dcaenc'
        'faac'
        'faad2'
        'fribidi'
        'jack2'
        'lame'
        'libdca'
        'libpulse'
        'libva'
        'libvpx'
        'libx264'
        'opencore-amr'
        'sqlite'
        'twolame'
        'x265'
        'xvidcore'
    )
    optdepends=('avidemux-qt-git: QT GUI')

    for target in 'core' 'core_plugins_common' 'core_plugins_settings' 'cli' 'cli_plugins'; do
        cd "${srcdir}/build/${target}"
        make DESTDIR="${pkgdir}" install
    done
}

package_avidemux-qt-git() {
    pkgdesc="Simple video editor (QT)"
    provides=('avidemux-qt')
    conflicts=('avidemux-qt')
    depends=('avidemux-cli-git' 'qt5-base' 'libxv' 'sdl' 'desktop-file-utils')
    install='avidemux.install'

    for target in 'qt' 'qt_plugins'; do
        cd "${srcdir}/build/${target}"
        make DESTDIR="${pkgdir}" install
    done

    install -D --mode='644' \
        "${srcdir}/${srcname}/avidemux2.desktop" \
        "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
    install -D --mode='644' \
        "${srcdir}/${srcname}/avidemux_icon.png" \
        "${pkgdir}/usr/share/pixmaps/avidemux-qt.png"
    sed --in-place \
        --expression='s/^Icon=avidemux$/Icon=avidemux-qt/' \
        --expression='s/^Exec=avidemux2_gtk$/Exec=avidemux3_qt5/' \
        --expression='s/^Name=avidemux2$/Name=Avidemux QT/' \
        "${pkgdir}/usr/share/applications/avidemux-qt.desktop"
}
