# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('namebench-git')
_srcname='namebench'
pkgdesc='DNS benchmarking tool'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/mwiora/${_srcname}"
license=('Apache')

depends=('python2' 'python2-dnspython' 'python2-graphy' 'python2-httplib2' 'python2-jinja' 'python2-simplejson')
makedepends=('git')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    export NO_THIRD_PARTY='true'
    python2 setup.py install --root="${pkgdir}" --optimize=1

    install --directory "${pkgdir}/usr/share"
    mv "${pkgdir}/usr/namebench" "${pkgdir}/usr/share/"
}
