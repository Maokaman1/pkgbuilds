pkgname=('python-evdev-git')
_srcname='python-evdev'
pkgdesc='Python bindings for the Linux input subsystem'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/gvalkov/${_srcname}"
license=('custom:BSD')

depends=('python' 'linux-api-headers')
makedepends=('git' 'python-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+https://github.com/gvalkov/${_srcname}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
