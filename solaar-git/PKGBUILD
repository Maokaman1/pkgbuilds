pkgname=('solaar-git')
_srcname='Solaar'
pkgdesc='Linux devices manager for the Logitech Unifying Receiver'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/pwr/${_srcname}"
license=('GPL2')

depends=('python' 'python-pyudev' 'python-gobject' 'pygtk')
makedepends=('git' 'python-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1

    install -D --mode='644' rules.d/42-logitech-unify-permissions.rules \
            "${pkgdir}/usr/lib/udev/rules.d/42-logitech-unify-permissions.rules"
}
