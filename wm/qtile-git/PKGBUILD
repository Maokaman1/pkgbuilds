# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('qtile-git')
_srcname='qtile'
pkgdesc='A small, flexible, scriptable tiling window manager written in Python'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/qtile/${_srcname}"
license=('MIT')

depends=('python' 'python-xcffib-git' 'pango' 'python-cairocffi')
optdepends=(
    'python-setproctitle: change the process name to qtile'
    'xcb-util-cursor: show the activity in desktop to qtile'
    'python-gobject: widget, notify'
    'python-dbus: widget, notify'
    'python-wifi: widget, wlan'
)
makedepends=('git' 'python-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
