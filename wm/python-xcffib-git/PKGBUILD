pkgname=('python-xcffib-git')
_srcname='xcffib'
pkgdesc='A drop-in replacement for xpyb based on cffi'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/tych0/${_srcname}"
license=('Apache')

depends=('python-six' 'python-cffi' 'libxcb')
makedepends=(
    'git'
    'python-setuptools'
    'python-cffi'
    'python-six'
    'libxcb'
    'ghc'
    'haskell-xcb-types'
    'haskell-language-python'
    'haskell-filemanip'
    'haskell-split'
    'haskell-mtl'
    'haskell-attoparsec'
    'haskell-hunit'
    'cabal-install'
    'haskell-test-framework'
    'haskell-test-framework-hunit'
    'haskell-optparse-applicative'
)
checkdepends=(
    'flake8'
    'parallel'
    'python-nose'
    'xorg-server-xvfb'
    'xorg-xeyes'
)
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+https://github.com/tych0/${_srcname}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    make
}
check() {
    cd "${srcdir}/${_srcname}"

    make check
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
