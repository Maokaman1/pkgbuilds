# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('j4-dmenu-desktop-git')
_srcname='j4-dmenu-desktop'
pkgdesc='A fast desktop menu'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/enkore/${_srcname}"
license=('GPL3')

depends=('dmenu')
makedepends=('git' 'cmake')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    cmake -DCMAKE_INSTALL_PREFIX='/usr' .
    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install
}
