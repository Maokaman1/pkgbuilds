pkgname=('bspwm-git')
_srcname='bspwm'
pkgver='r1'
pkgrel='1'
pkgdesc='A tiling window manager based on binary space partitioning'
arch=('i686' 'x86_64')
url="https://github.com/nfnty/${_srcname}"
license=('custom:BSD')

depends=('libxcb' 'xcb-util' 'xcb-util-wm' 'xcb-util-keysyms')
optdepends=('jansson')
makedepends=('git' 'jansson')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git#branch=json")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    make PREFIX='/usr'
}

package() {
    cd "${srcdir}/${_srcname}"

    make PREFIX='/usr' DESTDIR="${pkgdir}" install
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${_srcname}/LICENSE"
}
