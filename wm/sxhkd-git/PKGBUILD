pkgname=('sxhkd-git')
_srcname='sxhkd'
pkgver='r1'
pkgrel='1'
pkgdesc='Simple X hotkey daemon'
arch=('i686' 'x86_64')
url="https://github.com/baskerville/${_srcname}"
license=('custom:BSD')

depends=('libxcb' 'xcb-util-keysyms')
makedepends=('git' 'xcb-util')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    make PREFIX='/usr'
}

package() {
    cd "${srcdir}/${_srcname}"

    make PREFIX='/usr' DESTDIR="${pkgdir}" install
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
