pkgname=('wtftw-git')
_srcname='wtftw'
pkgver='r1'
pkgrel='1'
pkgdesc='Window Tiling For The Win. A tiling window manager written in Rust'
arch=('i686' 'x86_64')
url="https://github.com/Kintaro/${_srcname}"
license=('custom:BSD')

depends=('rust' 'libxinerama')
makedepends=('git' 'cargo')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    export CARGO_HOME="${srcdir}/cargo"
    cargo build --verbose --release
}

package() {
    cd "${srcdir}/${_srcname}"

    install -D --mode=755 target/release/wtftw "${pkgdir}/usr/bin/wtftw"
    install -D --mode=644 config/config.rs "${pkgdir}/usr/share/wtftw/config.rs"
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licences/wtftw/LICENSE"
}
