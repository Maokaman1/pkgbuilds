# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('pimd')
_srcname='pimd'
pkgdesc='PIM-SM multicast routing daemon'
pkgver='2.3.2'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/troglobit/${_srcname}"
license=('BSD')

depends=('')
makedepends=('git')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${_srcname}::git+${url}.git#tag=${pkgver}"
    'pimd.service'
    'noroot.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
)

backup=('etc/pimd.conf')

prepare() {
    cd "${srcdir}/${_srcname}"

    git submodule update --init

    git apply "${srcdir}/noroot.patch"
    sed -i 's#/sbin#/bin#g' Makefile
}

build() {
    cd "${srcdir}/${_srcname}"

    ./configure --prefix='/usr' --sysconfdir='/etc'

    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install

    install -D --mode=644 "${srcdir}/pimd.service" "${pkgdir}/usr/lib/systemd/system/pimd.service"
}
