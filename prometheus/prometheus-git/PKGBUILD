# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('prometheus-git')
_srcname='prometheus'
pkgdesc='Monitoring system and time series database'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/prometheus/${_srcname}"
license=('Apache')

makedepends=('git' 'go')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

_url_go="${url#*//}"

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    mv "${_srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${url}"
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    make build
}

package() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/"{"etc/${_srcname}",usr/bin,"var/lib/${_srcname}"}
    cp "${GOPATH}/src/${_url_go}/"{prometheus,promtool} "${pkgdir}/usr/bin/"

    cd "${GOPATH}/src/${_url_go}"
    cp config/testdata/conf.good.yml "${pkgdir}/etc/${_srcname}/config.yml"

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}"
}
