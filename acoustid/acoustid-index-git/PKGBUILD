# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('acoustid-index-git')
_srcname='acoustid-index'
pkgdesc='AcoustID Index'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://bitbucket.org/acoustid/${_srcname}"
license=('MIT')

depends=('qt4' 'icu')
makedepends=('git' 'cmake')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    mkdir --parents "${srcdir}/build"
    cd "${srcdir}/build"

    cmake \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        "${srcdir}/${_srcname}"

    make
}

package() {
    cd "${srcdir}/build"

    make DESTDIR="${pkgdir}" install
}
