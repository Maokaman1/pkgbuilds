# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('pg_acoustid-git')
_srcname='pg_acoustid'
pkgdesc='AcoustID utility functions for PostgreSQL'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://bitbucket.org/acoustid/${_srcname}"
license=('custom:postgresql')

depends=('postgresql')
makedepends=('git')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install
}
