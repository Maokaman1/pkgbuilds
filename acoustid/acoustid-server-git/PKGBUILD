# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('acoustid-server-git')
_srcname='acoustid-server'
pkgdesc='AcoustID Server'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://bitbucket.org/acoustid/${_srcname}"
license=('MIT')

depends=('python2' 'postgresql-libs' 'chromaprint')
makedepends=('git' 'python2-setuptools' 'python2-pip' 'python2-virtualenv')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${_srcname}::git+${url}.git"
    'libchromaprint.patch'
    'admin.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
)

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

prepare() {
    cd "${srcdir}/${_srcname}"

    git apply "${srcdir}/admin.patch"
}

package() {
    install --directory --mode='755' "${pkgdir}/opt/${_srcname}"
    cp --recursive "${srcdir}/${_srcname}/"* "${pkgdir}/opt/${_srcname}/"

    cd "${pkgdir}/opt/${_srcname}"
    virtualenv2 e
    # shellcheck disable=SC1091
    source e/bin/activate
    pip install --requirement requirements.txt

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}/opt/${_srcname}"

    cd e/lib/python2.7/site-packages
    patch --input="${srcdir}/libchromaprint.patch"
}
