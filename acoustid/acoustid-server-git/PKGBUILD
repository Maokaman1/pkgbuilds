pkgname=('acoustid-server-git')
srcname='acoustid-server'
pkgdesc='AcoustID Server'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://bitbucket.org/acoustid/${srcname}"
license=('MIT')

depends=('python2' 'postgresql-libs' 'chromaprint')
makedepends=('git' 'python2-setuptools' 'python2-pip' 'python2-virtualenv')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    install --directory --mode='755' "${pkgdir}/opt/${srcname}"
    cp --recursive "${srcdir}/${srcname}/"* "${pkgdir}/opt/${srcname}/"

    cd "${pkgdir}/opt/${srcname}"
    virtualenv2 e
    source e/bin/activate
    pip install --requirement requirements.txt

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}/opt/${srcname}"
}
