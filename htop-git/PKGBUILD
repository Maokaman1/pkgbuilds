# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('htop-git')
_srcname='htop'
pkgver='r1'
pkgrel='1'
pkgdesc='Interactive text-mode process viewer'
arch=('i686' 'x86_64')
url="https://github.com/hishamhm/${_srcname}"
license=('GPL2')

depends=('ncurses')
makedepends=('git' 'python')
optdepends=(
    'lsof: List open files for running process'
    'strace: Attach to running process'
)
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${_srcname}::git+${url}.git"
    'vim.patch'
    'starttime.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
)

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

prepare() {
    cd "${srcdir}/${_srcname}"

    git apply "${srcdir}/vim.patch"
    git apply "${srcdir}/starttime.patch"
}

build() {
    cd "${srcdir}/${_srcname}"

    autoreconf --install --force --warnings='all'
    ./configure --prefix='/usr' --enable-unicode

    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install
}
