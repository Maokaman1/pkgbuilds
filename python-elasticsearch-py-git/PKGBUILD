# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('python-elasticsearch-py-git')
_srcname='elasticsearch-py'
pkgdesc='Official Python low-level client for Elasticsearch'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/elastic/${_srcname}"
license=('Apache')

depends=('python' 'python-urllib3')
makedepends=('git' 'python-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
