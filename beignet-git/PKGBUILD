pkgname=('beignet-git')
_srcname='beignet'
pkgdesc='OpenCL Library for Intel Ivy Bridge and newer GPUs'
pkgver='r1'
pkgrel='1'
arch=('x86_64')
url='http://cgit.freedesktop.org/beignet'
license=('LGPL2')

depends=('libdrm' 'libxfixes' 'glu' 'libsm' 'libxext' 'mesa' 'ncurses' 'ocl-icd' 'llvm')
makedepends=('git' 'clang' 'cmake' 'python2')
provides=("${pkgname[0]%-git}" 'opencl-intel' 'opencl-headers' 'opencl-headers12')
conflicts=("${pkgname[0]%-git}" 'opencl-intel' 'opencl-headers' 'opencl-headers12')

source=("${_srcname}::git+git://anongit.freedesktop.org/beignet")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    cmake \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        -DCMAKE_BUILD_TYPE='RELEASE' \
        -DPYTHON_EXECUTABLE:FILEPATH='/usr/bin/python2' \
        .
    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install
}
