# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('bitcoin-classic')
_srcname='bitcoinclassic'
pkgdesc='Bitcoin Classic'
pkgver='1.1.1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/bitcoinclassic/${_srcname}"
license=('MIT')

depends=('boost-libs' 'openssl' 'libevent')
makedepends=('boost')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git#tag=v${pkgver}")
sha512sums=('SKIP')

build() {
    cd "${srcdir}/${_srcname}"

    autoreconf --install --force --warnings='all'
    ./configure --prefix='/usr' --with-incompatible-bdb --disable-wallet

    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install
}
