pkgname=('ranger-git')
_srcname='ranger'
pkgver='r1'
pkgrel='1'
pkgdesc='A simple, vim-like file manager'
arch=('any')
url="https://github.com/nfnty/${_srcname}"
license=('GPL3')

depends=('python')
optdepends=(
    'file: for determining file types'
    'python-chardet: in case of encoding detection problems'
    'sudo: to use the "run as root"-feature'
    'libcaca: for ASCII-art image previews'
    'highlight: for syntax highlighting of code'
    'atool: for previews of archives'
    'w3m: for previews of images and html pages'
    'lynx: for previews of html pages'
    'elinks: for previews of html pages'
    'poppler: for pdf previews'
    'transmission-cli: for viewing bittorrent information'
    'mediainfo: for viewing information about media files'
    'perl-image-exiftool: for viewing information about media files'
)
makedepends=('git')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${_srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
