# shellcheck disable=SC2034,SC2154,SC2164
pkgname=('vscode')
_srcname='vscode'
pkgdesc='Visual Studio Code'
pkgver='1.8.0'
pkgrel='1'
arch=('x86_64')
url="https://github.com/Microsoft/${_srcname}"
license=('MIT')

depends=('dbus' 'gtk2' 'libnotify' 'gconf' 'alsa-lib' 'libxtst' 'libxss' 'nss')
makedepends=('git' 'nodejs' 'npm' 'python2' 'gcc' 'gulp')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${_srcname}::git+${url}.git#tag=${pkgver}"
    "${pkgname[0]}.desktop"
)
sha512sums=(
    'SKIP'
    'SKIP'
)

_arch='x64'

build() {
    cd "${srcdir}/${_srcname}"

    ./scripts/npm.sh install --arch="${_arch}"

    gulp "vscode-linux-${_arch}-min"
}

package() {
    cd "${srcdir}/${_srcname}"

    install --directory "${pkgdir}/usr/"{bin,share}
    cp --recursive "${srcdir}/VSCode-linux-${_arch}" "${pkgdir}/usr/share/${pkgname[0]}"
    ln --symbolic "/usr/share/${pkgname[0]}/bin/code-oss" "${pkgdir}/usr/bin/${pkgname[0]}"

    install -D --mode='644' "${srcdir}/${pkgname[0]}.desktop" \
            "${pkgdir}/usr/share/applications/${pkgname[0]}.desktop"
}
