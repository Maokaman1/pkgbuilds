pkgname=('reddit-enhancement-suite-firefox-git')
_srcname='reddit-enhancement-suite-firefox'
pkgver=r4237.v4.5.1.opera.566.gdae0812
pkgrel=1
pkgdesc='A firefox extension that enhances your Reddit browsing experience'
arch=('any')
url='https://github.com/honestbleeps/Reddit-Enhancement-Suite'
license=('GPL3')

depends=('firefox')
makedepends=('git' 'unzip')
provides=('reddit-enhancement-suite-firefox')
conflicts=('reddit-enhancement-suite-firefox')

source=(
    "$_srcname::git+${url}.git"
    'https://ftp.mozilla.org/pub/mozilla.org/labs/jetpack/jetpack-sdk-latest.tar.gz'
    'python2.patch'
)
sha256sums=(
    'SKIP'
    'bb2d5d7c295a9d627cb02a4c5bb1123bf3e86ef01826c8246aeaf7912b52870c'
    'bb7bfe1906ac7bdfb64db21f836eabb5457511ba46cfc207f93cf862a94e363f'
)

EXTENSION_ID='jid1-xUfzOsOFlzSOXg@jetpack'
XPI_NAME='reddit_res.xpi'
ADDON_SDK_VER='1.17'

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

prepare() {
    cd "$srcdir/addon-sdk-${ADDON_SDK_VER}"
    patch --strip=1 --input="$srcdir/python2.patch"
}

build() {
    cd "$srcdir/$_srcname"
    bash 'makelinks.sh'
    cd "$srcdir/$_srcname/XPI"
    "$srcdir/addon-sdk-${ADDON_SDK_VER}/bin/cfx" xpi
}

package() {
    local destdir="${pkgdir}/usr/lib/firefox/browser/extensions/$EXTENSION_ID"
    install -d "$destdir"
    unzip -d "$destdir" "$srcdir/$_srcname/XPI/$XPI_NAME"
}
