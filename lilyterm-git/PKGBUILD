pkgname=('lilyterm-git')
_srcname='lilyterm'
pkgdesc='A lightweight, but functional terminal emulator'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/Tetralet/${_srcname}"
license=('GPL3')

depends=('vte3')
makedepends=('git' 'gettext' 'intltool' 'pkgconfig')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${_srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    cd "${srcdir}/${_srcname}"

    ./autogen.sh
    ./configure --prefix='/usr' --sysconfdir='/etc'

    make
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install
}
